apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'kotlin-kapt'

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

ext {
    versionMajor = 0
    versionMinor = 7
    versionPatch = 16
}

android {
    signingConfigs {
        playConfig {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file("$rootDir/../AndroidKeyStore/net_wuqs.jks")
            storePassword keystoreProperties['storePassword']
        }
    }
    compileSdkVersion 28
    defaultConfig {
        applicationId "net.wuqs.ontime"
        minSdkVersion 19
        targetSdkVersion 28
        versionCode genVersionCode()
        versionName genVersionName()
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation":
                                     "$projectDir/schemas".toString()]
            }
        }
        vectorDrawables.useSupportLibrary = true
    }
    buildTypes {
        release {
            minifyEnabled true
            debuggable false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.playConfig
        }
        debug {
            versionNameSuffix "-debug"
            debuggable true
            ext.alwaysUpdateBuildId = false
            applicationIdSuffix '.debug'
        }
    }
    productFlavors {
    }
    sourceSets {
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
    }
    buildToolsVersion '28.0.3'
}

private Integer genVersionCode() {
    return ext.versionMajor * 100000 + ext.versionMinor * 1000 + ext.versionPatch * 10
}

private String genVersionName() {
    String name = "${ext.versionMajor}.${ext.versionMinor}.${ext.versionPatch}"
    return name
}

dependencies {
    // Support libraries
    def appcompat_version = '1.0.2'
    def cardview_version = '1.0.0'
    def constraintlayout_version = '1.1.3'
    def gridlayout_version = '1.0.0'
    def recyclerview_version = '1.0.0'
    def vectordrawable_version = '1.0.1'
    def material_version = '1.0.0'

    // Architecture components
    def lifecycle_version = '2.0.0'
    def room_version = '2.1.0'

    // Test
    def junit_version = '4.12'

    // AndroidTest
    def espresso_version = '3.1.1'
    def android_junit_version = '1.1.0'
    def rules_version = '1.1.1'

    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    // Support libraries
    implementation "androidx.appcompat:appcompat:$appcompat_version"
    implementation "androidx.cardview:cardview:$cardview_version"
    implementation "androidx.constraintlayout:constraintlayout:$constraintlayout_version"
    implementation "androidx.gridlayout:gridlayout:$gridlayout_version"
    implementation "androidx.recyclerview:recyclerview:$recyclerview_version"
    implementation "androidx.vectordrawable:vectordrawable:$vectordrawable_version"
    implementation "com.google.android.material:material:$material_version"

    // Android KTX
    implementation "androidx.core:core-ktx:1.0.2"
    implementation "androidx.fragment:fragment-ktx:1.0.0"

    // Architecture components
    // ViewModel and LiveData
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycle_version"
    // Room
    implementation "androidx.room:room-runtime:$room_version"
    implementation 'androidx.preference:preference:1.1.0-rc01'
    kapt "androidx.room:room-compiler:$room_version"

    // Test
    testImplementation "junit:junit:$junit_version"

    // AndroidTest
    // Test helpers for Room
    androidTestImplementation "androidx.room:room-testing:$room_version"
    androidTestImplementation "androidx.test.espresso:espresso-core:$espresso_version"
    androidTestImplementation "androidx.test.ext:junit:$android_junit_version"
    androidTestImplementation "androidx.test:rules:$rules_version"
}
